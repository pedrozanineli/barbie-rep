* Calculates the volume of a selection, and other statistics
* Example usage:
*   stream getvol.str all 0     ! actual volume of everything
*   stream getvol.str MYSEL 1.4 ! use defined selection MYSEL and add 1.4
* required positional args:
*   IN1     atom-selection  atoms for which to calculate volume (required)
*   IN2     int             amount to add to radius for volume calculation
*                           (recommended default: 0)
*

coor stat select @IN1 end
set nsel = ?nsel

calc dcel = 0.5
calc xdim = int ( ( ?xmax - ?xmin + 5.0 ) / @dcel ) + 1
calc ydim = int ( ( ?ymax - ?ymin + 5.0 ) / @dcel ) + 1
calc zdim = int ( ( ?zmax - ?zmin + 5.0 ) / @dcel ) + 1
calc space = @xdim * @ydim * @zdim

! prevent overflow on integer conversion
calc maxint = 2**32 / 2 - 1
if space .gt. @maxint calc space = @maxint

scalar 1 = radius
scalar 1 add @IN2
scalar 2 set 6.0

coor volume hole space @space select @IN1 end
calc volume = ?volume

calc radius = ( @volume * 3/4/?pi )**(1/3)

if nsel .eq. 1 return

coor rgyr select @IN1 end
calc radius0 = ?rgyr * 1.2

if radius .lt. @{radius0} set radius = @radius0

